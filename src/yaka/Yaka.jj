/*
 *
 *  Auteur: Pedrono Marie-Jo
 *
 * 	création : 13 février 2002
 *
 *  nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)

package yaka;

import yaka.Constante;
import operation.*;

public class Yaka {
  public static Affectation affectation;
  public static Declaration declaration;
  public static Entree entree;
  public static ErreurManager em;
  public static Expression expression;
  public static Sortie sortie;
  public static TabIdent tabIdent;
  public static YVM yvm;
  public static boolean init = false;

  public static void main(String args[]) { 
    Yaka.affectation = new Affectation();
    Yaka.declaration = new Declaration();
    Yaka.entree = new Entree();
    Yaka.em = new ErreurManager();
    Yaka.expression = new Expression();
    Yaka.sortie = new Sortie();
    Yaka.tabIdent = new TabIdent(2);
    java.io.InputStream input;
    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+".yaka");
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
      Yaka.yvm = new YVM(args[args.length-1]);
    } else {
      System.out.println("Usage: java Yaka fichier");
      return;
    }
    if(!init) {
    	new Yaka(input);
    	init = true;
    }
    else
    	Yaka.ReInit(input);
    try {
      Yaka.analyse();
      System.out.println("analyse syntaxique reussie!");
    } catch (ParseException e) {
      String msg = e.getMessage();
      msg = msg.substring(0,msg.indexOf("\u005cn"));
      System.out.println("Erreur de syntaxe : "+msg);
    }
    // si aucune erreur, on passe à la génération du code assembleur
    if(!Yaka.em.hasErreur()) {
    	// il fat réouvrir le fichier...
        try {
			input.close();
			input = new java.io.FileInputStream(args[args.length-1]+".yaka");
        } catch (java.io.IOException e1) {
        	System.out.println("N'a pas pu reouvrir le fichier pour la seconde passe");
        	return;
		}
		IdVar.reInit();
		Yaka.ReInit(input);
        Yaka.yvm = new YVMasm(args[args.length-1]);
        try {
              Yaka.analyse();
            } catch (ParseException e) {
              System.out.println(e);
            }
    }
    }
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots réservés*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{
   (<PROGRAMME> {Yaka.yvm.entete();}) <ident>	
   bloc()
   (<FPROGRAMME> {Yaka.yvm.queue();})
}

void bloc() : {}{
 (declConst())*
 (declVar())* {Yaka.tabIdent.ouvrePrinc();}		
   suiteInstr() 
}

void declConst() : {}
{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
	
  (<ident> { Yaka.declaration.nouvelleConstante(YakaTokenManager.identLu); })
   "=" valConst()
    
}

void valConst() : {}{
  (<entier> { Yaka.declaration.majValeur(YakaTokenManager.entierLu); })
 | (<ident>	{ Yaka.declaration.majValeur(YakaTokenManager.identLu); })
 | (<VRAI>  { Yaka.declaration.majValeur(true); })
 | (<FAUX>  { Yaka.declaration.majValeur(false); })
}


void declVar() : {}
{
  <VAR> type()	
  (<ident> { Yaka.declaration.nouvelleVariable(YakaTokenManager.identLu); })
  ("," 
      (<ident> { Yaka.declaration.nouvelleVariable(YakaTokenManager.identLu); }) 
  )* ";"
	
}

void type() :{}
{
   (<ENTIER> { Yaka.declaration.majType(Constante.ENTIER); })			
 | (<BOOLEEN> { Yaka.declaration.majType(Constante.BOOLEEN); })
}
/*
 * Syntaxe des instructions.
 */

void suiteInstr() : {}
{
	instruction()
	(";" (instruction())? )*
}  


void instruction() : {}
{
	affectation() | lecture() | ecriture()
}

void affectation() : {}
{
	(<ident> { Yaka.affectation.setAffecterDans(YakaTokenManager.identLu); }) "=" (expression() { Yaka.affectation.faitAffectation(); })
}

void lecture() : {}
{
	<LIRE> "(" (<ident> { Yaka.entree.lireIdent(YakaTokenManager.identLu); }) ")"
}

void ecriture() : {}
{
	(<ECRIRE> "(" ((expression() { Yaka.sortie.ecrireTete(); }) | (<chaine> { Yaka.yvm.ecrireChaine(YakaTokenManager.chaineLue); })) ")") | (<ALALIGNE> { Yaka.yvm.aLaLigne(); })
}

/*
 * Expression.
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() { Yaka.expression.faireOperation(); }
  )?
}



void simpleExpr() : {}
{
   terme() 
   (opAdd() 
   terme() { Yaka.expression.faireOperation(); }
   )*
} 
void terme() : {}
{
  facteur() 
  (opMul() 
  facteur() { Yaka.expression.faireOperation(); }
  )*
}
void facteur() : {}
{   primaire() 
  |  (opNeg() primaire() { Yaka.expression.faireOperation(); })
}

void primaire (): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur () : {}
{
    (<entier> { Yaka.expression.ajouteValeur(YakaTokenManager.entierLu); })
  | (<ident> { Yaka.expression.ajouteValeur(YakaTokenManager.identLu); })
  | (<VRAI> { Yaka.expression.ajouteValeur(true);})
  | (<FAUX> { Yaka.expression.ajouteValeur(false); })
}

void opRel() : {}
{
    ("=" { Yaka.expression.ajouteOperation(new Egal()); })
  | ("<>" { Yaka.expression.ajouteOperation(new Diff()); })
  | ("<" { Yaka.expression.ajouteOperation(new Inf()); })	
  | ("<=" { Yaka.expression.ajouteOperation(new InfEgal()); })
  | (">" { Yaka.expression.ajouteOperation(new Sup()); })
  | (">=" { Yaka.expression.ajouteOperation(new SupEgal()); })
} 

void opAdd() : {}
{
    ("+" { Yaka.expression.ajouteOperation(new Add()); })
  | ("-" { Yaka.expression.ajouteOperation(new Sub()); })
  | (<OU> { Yaka.expression.ajouteOperation(new Or()); })
}

void opMul() : {}
{
  	("*" { Yaka.expression.ajouteOperation(new Mul()); })
  | ("/" { Yaka.expression.ajouteOperation(new Div()); })
  | (<ET> { Yaka.expression.ajouteOperation(new And()); })
} 

void opNeg() : {}
{
	("-" { Yaka.expression.ajouteOperation(new Moins()); })
  | (<NON> { Yaka.expression.ajouteOperation(new Non()); })
}


